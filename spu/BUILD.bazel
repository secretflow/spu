# Copyright 2021 Ant Group Co., Ltd.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@pybind11_bazel//:build_defs.bzl", "pybind_extension")
load("@rules_python//python:defs.bzl", "py_library")
load("@com_github_grpc_grpc//bazel:python_rules.bzl", "py_proto_library")
load("@rules_python//python:packaging.bzl", "py_package", "py_wheel")
load("//bazel:spu.bzl", "spu_version_file")
load("@pip_deps//:requirements.bzl", "all_deps")

package(default_visibility = ["//visibility:public"])

exports_files(
    [
        "exported_symbols.lds",
        "version_script.lds",
    ],
    visibility = ["//visibility:private"],
)

pybind_extension(
    name = "libspu",
    srcs = ["libspu.cc"],
    linkopts = select({
        "@bazel_tools//src/conditions:darwin": [
            "-Wl,-exported_symbols_list,$(location //spu:exported_symbols.lds)",
        ],
        "//conditions:default": [
            "-Wl,--version-script,$(location //spu:version_script.lds)",
        ],
    }),
    deps = [
        ":exported_symbols.lds",
        ":version_script.lds",
        "//libspu/compiler:compile",
        "//libspu/compiler/common:compilation_context",
        "//libspu/core:logging",
        "//libspu/device:api",
        "//libspu/device:io",
        "//libspu/device/pphlo:pphlo_executor",
        "//libspu/psi:bucket_psi",
        "//libspu/psi:memory_psi",
        "@yacl//yacl/link",
    ],
)

py_proto_library(
    name = "spu_py_proto",
    deps = ["//libspu:spu_proto"],
)

py_proto_library(
    name = "psi_py_proto",
    deps = ["//libspu/psi:psi_proto"],
)

py_library(
    name = "api",
    srcs = ["api.py"],
    data = [
        ":libspu.so",
    ],
    deps = [
        ":spu_py_proto",
    ],
)

py_library(
    name = "psi",
    srcs = ["psi.py"],
    data = [
        ":libspu.so",
    ],
    deps = [
        ":psi_py_proto",
    ],
)

SPU_VERSION = "0.3.2b4"

py_library(
    name = "init",
    srcs = [
        "__init__.py",
        ":api",
        ":psi",
        ":psi_py_proto",
        ":spu_py_proto",
        ":version_file",
        "//spu/utils:simulation",
    ],
)

py_package(
    name = "_lib_deps",
    deps = [
        ":api",
    ],
)

spu_version_file(
    name = "version_file",
    filename = "version.py",
    version = SPU_VERSION,
)

py_wheel(
    name = "spu_wheel",
    abi = "cp38",
    author = "secretflow",
    distribution = "spu",
    license = "Apache 2.0",
    platform = select({
        "@bazel_tools//src/conditions:darwin_x86_64": "macosx_10_16_x86_64",
        "@bazel_tools//src/conditions:darwin_arm64": "macosx_11_0_arm64",
        "//conditions:default": "manylinux2014_x86_64",
    }),
    python_requires = "==3.8.*",
    python_tag = "cp38",
    requires = all_deps,
    version = SPU_VERSION,
    deps = [
        ":_lib_deps",
        ":init",
        "//spu/utils:distributed",
        "//spu/utils:distributed_py_proto",
        "//spu/utils:distributed_py_proto_grpc",
        "//spu/utils:frontend",
        "//spu/utils:simulation",
    ],
)
